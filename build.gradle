plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'edu.sc.seis.macAppBundle' version '2.1.0'
}

compileJava {
    options.fork (memoryMaximumSize: '1024m') 
}

mainClassName = 'com.mucommander.muCommander'

allprojects {
    group = 'org.mucommander'
    version = '0.9.1'
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

repositories.jcenter()

dependencies {
    compile project('mucommander-commons-file')
    compile project('mucommander-commons-conf')
    compile project('mucommander-commons-collections')

    compile 'org.jmdns:jmdns:3.5.0'
    compile 'org.slf4j:slf4j-api:1.7.13'
    compile 'ch.qos.logback:logback-core:1.1.3'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'com.jidesoft:jide-oss:3.6.12'
    compile files('libs/java-extension.jar')

    testCompile 'org.testng:testng:6.9.10'
    testCompile 'junit:junit:4.12'
}

jar {
    manifest {
        attributes("Main-Class": mainClassName,
                   "Specification-Title": "muCommander",
                   "Specification-Vendor": "Arik Hadas",
                   "Specification-Version": version,
                   "Implementation-Title": "muCommander",
                   "Implementation-Vendor": "Arik Hadas",
                   "Implementation-Version": "1a32b2c",
                   "Build-Date": new Date().format('yyyyMMdd'),
                   "Build-URL": "http://www.mucommander.com/version/version.xml")
    }
}

macAppBundle {
    appName = "muCommander"
    mainClassName = "com.mucommander.muCommander"
    appStyle = "Apple"
    jvmVersion = "1.6+"
    icon = "package/osx/icon.icns"
    bundleAllowMixedLocalizations = "true"
    bundleExtras.put("NSHighResolutionCapable", "true")
    bundleExtras.put("NSSupportsAutomaticGraphicsSwitching", "true")
    backgroundImage = "package/osx/bg.gif"
    backgroundImageWidth = 450
    backgroundImageHeight = 475
    appIconX = 225
    appIconY = 75
    appFolderX = 225
    appFolderY = 350

    javaProperties.put("java.system.class.loader", "com.mucommander.commons.file.AbstractFileClassLoader")
    javaProperties.put("com.apple.smallTabs", "true")
    javaProperties.put("com.apple.hwaccel", "true")
    javaProperties.put("apple.laf.useScreenMenuBar", "true")
    javaProperties.put("file.encoding", "UTF-8")
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude 'com/ibm/icu/impl/**'
    exclude 'com/ibm/icu/lang/**'
    exclude 'com/ibm/icu/math/**'
    exclude 'com/ibm/icu/util/**'
    exclude 'javax/el/**'
    exclude 'javax/servlet/**'
    exclude 'com/sun/jna/darwin/**'
    exclude 'com/sun/jna/freebsd-x86/**'
    exclude 'com/sun/jna/freebsd-x86-64/**'
    exclude 'com/sun/jna/linux-arm/**'
    exclude 'com/sun/jna/linux-x86/**'
    exclude 'com/sun/jna/linux-x86-64/**'
    exclude 'com/sun/jna/openbsd-x86/**'
    exclude 'com/sun/jna/openbsd-x86-64/**'
    exclude 'com/sun/jna/platform/**'
    exclude 'com/sun/jna/sunos-sparc/**'
    exclude 'com/sun/jna/sunos-sparcv9/**'
    exclude 'com/sun/jna/sunos-x86/**'
    exclude 'com/sun/jna/sunos-x86-64/**'
    exclude 'com/sun/jna/w32ce-arm/**'
    exclude 'org/eclipse/**'
    exclude 'jdtCompilerAdapter.jar'
}



